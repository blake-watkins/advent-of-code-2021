(in-package :aoc-2021)

(defun map-operator (op)
  (case op
    (0 :sum)
    (1 :product)
    (2 :min)
    (3 :max)
    (4 :num)
    (5 :gt)
    (6 :lt)
    (7 :eq)))

(defun parse-bit ()
  (parse-digit))

(defun parse-number-field  (len)
  (with-monad
    (assign bits (n-of len (parse-bit)))
    (unit (digits-to-int bits :base 2))))

(defun parse-number-packet (&optional (acc 0))
  (with-monad
    (assign bits (n-of 5 (parse-bit)))
    (let ((new-acc (+ (* acc 16) (digits-to-int (subseq bits 1)))))
      (if (= 0 (elt bits 0))
          (unit new-acc)
          (parse-number-packet new-acc)))))

(defun parse-operator-packet ()
  (with-monad
    (assign length-type-id (parse-bit))
    (if (= length-type-id 0)
        (with-monad
          (assign length (parse-number-field 15))
          (assign sub-packets (n-of length (parse-bit)))
          (unit (run-parser (parse-packets) (format nil "狺篚猸疳汶弭螬┅鏖翳盹钺ㄡ篌殓戾铉翳疳蝮瀛铛礅弪骈屐北┅ㄡ篌殓篚猸疳汶弭瞽镦戾铉翳疳蝮瀛疳汶弭┅躅轸篚猸疳汶弭螬┅┅ㄤ彐躅疳蝮瀛疳汶弭ī疳蝮瀛扉篝疳蝮瀛疳汶弭┅ㄤ彐躅疳蝮瀛疳汶弭ī鏖翳盹钺ㄡ篌殓鲥蝮轱疳蝮瀛铛礅弪骈屐畅ㄡ篌殓豉疱殇疳蝮瀛铛礅弪骈屐畅ㄡ篌殓疳汶弭ㄩ豉疱殇穿疳蝮瀛铛礅弪疳汶弭疳蝮瀛镳弪狒矧疳汶弭┅躅轸扉篝鲥蝮轱磲瓠镳弪狒矧豉疱殇疳汶弭┅┅ㄤ彐躅徜洵鲥蝮轱铙疳汶弭ㄣ镱è羼侯蹴箦泔钿疳汶弭┅ㄦ轵篝疳汶弭┅ǐㄦ轵篝疳汶弭蝈漉沐＇磲疸狎＇徜洵鲥蝮轱铙翳轵疳汶弭┅┅┅ㄤ彐躅弼犰疳汶弭疳汶弭ㄤ弩趄蹉趱蜷铉忾钿鲥镳泔溴溽翎疳汶弭ㄤ邈灬蝈ㄩ珙矧鲥颟ㄩㄥ镳泔溴侯蹴溽翎戾舄è镳ㄣ狍镳泔溴ê篚＇ê痱镤蹉＇ê黹＇黹瞟ê磲＇磲ê玺＇京ê祠＇缉ê羼＇僵┅蝈蝈漉沐镳磲疸狎＇弼犰疳汶弭溽翎┅┅ㄩㄦ轭镳泔溴Ж虹红哄瘵ㄩ蝈癌蝈舂┅┅ㄤ彐躅溽倍ㄩ铕豸脲疳螋博戾舄è忾趔ㄦ矧磲铋漫ア疳蝮瀛轭翦珏轭瘐候徜轼倍┅疳汶弭蝓瞽疳蝮弪疳蝮瀛疳汶弭忾趔┅ㄩ疳螋暴ㄡ滗鲥蝮轱铙疳汶弭ㄥ鲠飙疳汶弭疳汶弭┅┅